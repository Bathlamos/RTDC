apply plugin: 'war'
apply plugin: 'cargo'
apply plugin: 'jetty'
apply plugin: 'gwt'
apply plugin: 'eclipse'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:0.6.1'
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

repositories { mavenCentral() }

dependencies {
    def cargoVersion = '1.4.4'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"
    // Needed for Legacy SuperDevMode
    //	gwt files('etc/superdevmode-launcher-legacy.jar')
}

gwt {
    gwtVersion='2.7.0'

    modules 'de.richsource.gradle.plugins.gwt.example.Example'
    devModules 'de.richsource.gradle.plugins.gwt.example.ExampleDev'

    maxHeapSize = "1024M"

    superDev {
        noPrecompile=true
    }
}

// Needed for Legacy SuperDevMode
//gwtDev {
//	args '-superDevMode'
//}

cargo {
    containerId = 'jetty9x'
    port = 8080

    deployable {
        file = tasks.draftWar.archivePath
        context = 'app'
    }

    local {
        installer {
            installUrl = file("$projectDir/etc/jetty-distribution-9.0.5.v20130815.zip").toURI()
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}

task jettyDraftWar(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
}

afterEvaluate {
    tasks.cargoStartLocal.dependsOn(tasks.draftWar)
    tasks.cargoRunLocal.dependsOn(tasks.draftWar)
}