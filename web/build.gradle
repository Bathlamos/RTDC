buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        //GWT plugin
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:0.6.1'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'cargo'
apply plugin: 'jetty'
apply plugin: 'gwt'

evaluationDependsOn(":core")

dependencies {
    def cargoVersion = '1.4.4'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"

    //We use this notation to include the source, for GWT
    compile files(project(':core').sourceSets.main.java.srcDirs)

    compile 'org.hibernate:hibernate-validator:4.1.0.Final'
    gwt group: 'org.hibernate', name: 'hibernate-validator', version: '4.1.0.Final', classifier: 'sources'
    compile 'javax.validation:validation-api:1.0.0.GA'
    gwt group: 'javax.validation', name: 'validation-api', version: '1.0.0.GA', classifier: 'sources'
    //Dependencies of Hibernate validator
    compile 'org.slf4j:slf4j-api:1.6.1'
    gwt group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1', classifier: 'sources'
    compile 'org.slf4j:slf4j-log4j12:1.6.1'
    gwt group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1', classifier: 'sources'
    compile 'log4j:log4j:1.2.16'
    gwt group: 'log4j', name: 'log4j', version: '1.2.16', classifier: 'sources'

    compile 'com.google.inject:guice:4.0-beta5'
    compile 'com.google.guava:guava-gwt:18.0'
    compile 'com.google.inject.extensions:guice-servlet:4.0-beta5'
    compile 'com.google.gwt:gwt-servlet:2.7.0'
    compile 'com.google.gwt:gwt-user:2.7.0'
    compile 'org.hibernate:hibernate-core:4.3.8.Final'
    compile 'com.sun.jersey:jersey-server:1.19'
    compile 'com.sun.jersey.contribs:jersey-guice:1.19'
    compile 'com.owlike:genson:1.2'
    compile 'javax.el:javax.el-api:3.0.0' //A dependency of Hibernate Validator
    compile 'postgresql:postgresql:9.1-901-1.jdbc4'
    compile 'org.mindrot:jbcrypt:0.3m'
}

gwt {
    gwtVersion='2.7.0'
    modules 'rtdc.web.rtdc-web'

    maxHeapSize = "1024M"

    superDev {
        noPrecompile=true
    }
}

cargo {
    containerId = 'jetty9x'
    port = 8080

    deployable {
        file = tasks.draftWar.archivePath
        context = 'app'
    }

    local {
        installer {
            installUrl = file("$projectDir/etc/jetty-distribution-9.0.5.v20130815.zip").toURI()
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}

task jettyDraftWar(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
}

afterEvaluate {
    tasks.cargoStartLocal.dependsOn(tasks.draftWar)
    tasks.cargoRunLocal.dependsOn(tasks.draftWar)
}